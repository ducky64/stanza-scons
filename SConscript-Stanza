import shlex, subprocess
from collections import namedtuple
import os

Import('env')

AddOption('--stanzac',
          dest='stanzac',
          type='string',
          action='store',
          help='custom stanza executable')

AddOption('--stanzadir',
          dest='stanzadir',
          type='string',
          action='store',
          help='(temporary) path to stanza install, needed for macro compilation')

if GetOption('stanzac'):
  env['STANZAC'] = GetOption('stanzac')
  print("Using specified STANZAC='%s'" % env['STANZAC'])
else:
  env['STANZAC'] = 'stanza'
  print("Using default STANZAC=%s" % env['STANZAC'])

stanza_config_file = open(os.path.join(os.path.expanduser('~'), '.stanza'), 'r')

found = False
for line in stanza_config_file:
  line_split = line.split('=')
  line_split = [elt.strip() for elt in line_split]
  line_split = [elt.strip('"') for elt in line_split]
  if line_split[0] == 'install-dir':
    assert not found, "multiple install-dir found in .stanza config"
    env['STANZADIR'] = line_split[1]
    found = True
assert found, "no install-dir found in .stanza config"

env['STANZA_FLAGS'] = []

##
## Stanza Compiler Builder
##
stanza_compiler_main = "compiler/stz-main.stanza"

env['STANZA_COMPILER_MAIN'] = os.path.join(env['STANZADIR'], stanza_compiler_main)

env.Append(BUILDERS = {
  # Compiles a Stanza compiler with custom macro syntax
  'StanzaCompilerBuild': Builder(
    action='$STANZAC $SOURCES $STANZA_COMPILER_MAIN -o $TARGET -s ${TARGET}_src.s $STANZA_FLAGS -optimize',
    src_suffix='.stanza',
  )
})

"""Builds a Stanza compiler with additional sources for macros.
An intermediate target (the compiler binary) is generated, and the final out
is just that wrapped in a script generated by Stanza's installer.
"""
def StanzaCompiler(env, target, source):
  return env.StanzaCompilerBuild(File(target), source)

env.AddMethod(StanzaCompiler)

##
## Stanza Builder
##

StanzaLibType = namedtuple('StanzaLibType', ['Sources'])

env['STANZA_LIBS'] = []

# A simple wrapper around source files for a static library, since Stanza has
# no concept of a static library currently.
# TODO: replace with Stanza static libraries when ready
def StanzaLib(env, target, source):
  assert isinstance(source, list)
  return StanzaLibType(source)
env.AddMethod(StanzaLib)

# Compiles a Stanza program
_stanza_builder = Builder(
  action='$STANZAC $SOURCES -o $TARGET -s ${TARGET}_src.s $STANZA_FLAGS',
  src_suffix='.stanza')

def Stanza(env, target, source, stanzac=None):
  if stanzac is not None:
    env = env.Clone(
      STANZAC=stanzac,
    )
    Depends(target, stanzac)
  if env['STANZA_LIBS']:
    source = sum([lib.Sources for lib in env['STANZA_LIBS']], []) + source
  return _stanza_builder.__call__(env, target, source)

env.AddMethod(Stanza)
