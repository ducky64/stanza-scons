import shlex, subprocess
from collections import namedtuple
import os
import warnings

Import('env')

if 'STANZAC' not in env:
  env['STANZAC'] = 'stanza'

if 'STANZA_CONFIG' not in env:
  if 'STANZA_CONFIG' in env['ENV']:
    env['STANZA_CONFIG'] = env['ENV']['STANZA_CONFIG']
  if 'STANZA_CONFIG' in os.environ:
    env['STANZA_CONFIG'] = os.environ['STANZA_CONFIG']
  else:
    warnings.warn("No STANZA_CONFIG detected, falling back to homedir")
    env['STANZA_CONFIG'] = os.path.expanduser('~')
stanza_config_file = open(os.path.join(env['STANZA_CONFIG'], '.stanza'), 'r')

for line in stanza_config_file:
  line_split = line.split('=')
  line_split = [elt.strip() for elt in line_split]
  line_split = [elt.strip('"') for elt in line_split]
  if line_split[0] == 'install-dir':
    assert 'STANZADIR' not in env, "multiple install-dir found in .stanza config"
    env['STANZADIR'] = line_split[1]
assert 'STANZADIR' in env, "no install-dir found in .stanza config"

env['STANZA_FLAGS'] = []

if 'STANZAFILESUFFIX' not in env:
  env['STANZAFILESUFFIX'] = '.stanza'

if 'STANZAPROGSUFFIX' not in env:
  if env['PLATFORM'] == 'win32':
    # Stanza seems to add a .exe suffix onto compiled programs for some reason.
    env['STANZAPROGSUFFIX'] = '.exe'
  else:
    env['STANZAPROGSUFFIX'] = ''

# Propagate some required environment variables from OS
# TODO: do this in a similar way to other SCons tools
if 'TEMP' not in env['ENV'] and 'TEMP' in os.environ:
  # Required for MinGW builds
  env['ENV']['TEMP'] = os.environ['TEMP']
if 'STANZA_CONFIG' not in env['ENV'] and 'STANZA_CONFIG' in os.environ:
  env['ENV']['STANZA_CONFIG'] = os.environ['STANZA_CONFIG']
if 'HOME' not in env['ENV'] and 'HOME' in os.environ:
  env['ENV']['HOME'] = os.environ['HOME']

##
## Stanza Builder
##

if 'STANZACOM' not in env:
  env['STANZACOM'] = '$STANZAC $SOURCES -o $TARGET -s ${TARGET}_src.s $STANZA_FLAGS'

if 'STANZACOMSTR' not in env:
  env['STANZACOMSTR'] = '$STANZACOM'

StanzaLibType = namedtuple('StanzaLibType', ['Sources'])

env['STANZA_LIBS'] = []

# A simple wrapper around source files for a static library, since Stanza has
# no concept of a static library currently.
# TODO: replace with Stanza static libraries when ready
def StanzaLib(env, target, sources):
  assert isinstance(sources, list)
  sources = [File(source) for source in sources]
  return StanzaLibType(sources)
env.AddMethod(StanzaLib)

# Compiles a Stanza program
_stanza_builder = Builder(
  action=Action('$STANZACOM', '$STANZACOMSTR'),
  src_suffix='$STANZAFILESUFFIX',
  suffix='$STANZAPROGSUFFIX')

def Stanza(env, target, source, stanzac=None):
  if stanzac is not None:
    env = env.Clone(
      STANZAC=stanzac,
    )
  if env['STANZA_LIBS']:
    source = sum([lib.Sources for lib in env['STANZA_LIBS']], []) + source

  # Calling this early makes sure the stanzac dependency is named correctly
  # for example, if a program suffix is needed
  target = _stanza_builder.__call__(env, target, source)

  if stanzac is not None:
    Depends(target, stanzac)
  return target

env.AddMethod(Stanza)

##
## Stanza Compiler Builder
##
stanza_compiler_main = "compiler/stz-main.stanza"
env['STANZA_COMPILER_MAIN'] = File(os.path.join(env['STANZADIR'], stanza_compiler_main))

"""Builds a Stanza compiler with additional sources for macros.
An intermediate target (the compiler binary) is generated, and the final out
is just that wrapped in a script generated by Stanza's installer.
"""
def StanzaCompiler(env, target, source):
  env = env.Clone()
  env.Append(STANZA_FLAGS = '-optimize')
  source = source + [env['STANZA_COMPILER_MAIN']]
  return env.Stanza(target, source)

env.AddMethod(StanzaCompiler)
